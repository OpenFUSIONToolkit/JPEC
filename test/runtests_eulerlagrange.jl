using TOML
using LinearAlgebra

# TODO: this helper may belong in a shared test-utilities file rather than here.
# TODO: come up with a Gaussian reduction test that doesn't rely on external data.

function load_u_matrix(filename)
    lines = readlines(filename)
    data = [parse.(Float64, split(l)) for l in lines[2:end]]
    i_vals = Int.(getindex.(data, 1))
    j_vals = Int.(getindex.(data, 2))
    ncols = (length(data[1]) - 2) ÷ 2
    imax = maximum(i_vals)
    jmax = maximum(j_vals)
    mat = zeros(ComplexF64, imax, jmax, ncols)
    for row in data
        i = Int(row[1])
        j = Int(row[2])
        for k in 1:ncols
            re = Float64(row[2*k+1])
            im = Float64(row[2*k+2])
            mat[i, j, k] = complex(re, im)
        end
    end
    return mat
end

@testset "ODE Tests" begin
    @testset "resize_storage!" begin
        # Test that resize_storage! doubles the size of storage arrays
        mpert = 3
        numsteps_init = 10
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(mpert, numsteps_init, 10, 5)

        # Fill some data
        odet.step = 8
        for i in 1:odet.step
            odet.psi_store[i] = Float64(i)
            odet.q_store[i] = Float64(i * 2)
            odet.u_store[:, :, :, i] .= ComplexF64(i)
        end

        # Resize storage
        GeneralizedPerturbedEquilibrium.ForceFreeStates.resize_storage!(odet)

        # Check new size is doubled
        @test length(odet.psi_store) == 2 * numsteps_init
        @test length(odet.q_store) == 2 * numsteps_init
        @test size(odet.u_store, 4) == 2 * numsteps_init

        # Derivative stores are materialized after integration, so growth never touches them
        @test isempty(odet.du_store)
        @test isempty(odet.xi_s_store)

        # Check data is preserved
        @test all(odet.psi_store[1:odet.step] .== Float64.(1:odet.step))
        @test all(odet.q_store[1:odet.step] .== Float64.(2:2:(2*odet.step)))
        for i in 1:odet.step
            @test all(odet.u_store[:, :, :, i] .== ComplexF64(i))
        end

        # Check that you can resize again
        GeneralizedPerturbedEquilibrium.ForceFreeStates.resize_storage!(odet)
        @test length(odet.psi_store) == 4 * numsteps_init
        @test length(odet.q_store) == 4 * numsteps_init
        @test size(odet.u_store, 4) == 4 * numsteps_init
    end

    @testset "trim_storage!" begin
        # Test that trim_storage! resizes arrays to actual step count
        mpert = 3
        numsteps_init = 20
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(mpert, numsteps_init, 10, 5)

        # Set step to less than initial size
        odet.step = 12
        for i in 1:odet.step
            odet.psi_store[i] = Float64(i)
            odet.q_store[i] = Float64(i * 2)
            odet.u_store[:, :, :, i] .= ComplexF64(i)
        end

        # Trim storage
        GeneralizedPerturbedEquilibrium.ForceFreeStates.trim_storage!(odet)

        # Check sizes match step count
        @test length(odet.psi_store) == odet.step
        @test length(odet.q_store) == odet.step
        @test size(odet.u_store, 4) == odet.step

        # Check all data is preserved
        @test all(odet.psi_store .== Float64.(1:odet.step))
        @test all(odet.q_store .== Float64.(2:2:(2*odet.step)))
    end

    @testset "transform_u!" begin
        # Test transformation of solution vectors
        mpert = 2
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; mpert=mpert, numpert_total=mpert)
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(mpert, 10, 5, 2)

        # Set up a simple fixup scenario
        odet.ifix = 1
        odet.step = 5
        odet.sing_flag[1] = false
        odet.fixstep[1] = 3
        odet.zeroed_idx[1] = Int[]

        # Initialize fixfac with some transformation
        odet.fixfac[1, 1, 1] = 1.0
        odet.fixfac[1, 2, 1] = 0.5
        odet.fixfac[2, 1, 1] = 0.0
        odet.fixfac[2, 2, 1] = 1.0

        # Initialize index (sorted by unorm)
        odet.index[:, 1] = [1, 2]

        # Set up some u_store data; derivative stores stay empty on this path
        for i in 1:odet.step
            odet.u_store[:, :, 1, i] .= ComplexF64(i)
            odet.u_store[:, :, 2, i] .= ComplexF64(i + 0.1)
        end

        u_orig = copy(odet.u_store)

        # Apply transformation
        GeneralizedPerturbedEquilibrium.ForceFreeStates.transform_u!(odet, intr)

        # Check that u_store was modified (transformation applied)
        @test !all(odet.u_store .== u_orig)

        # The transformation should preserve the structure but apply the fixfac matrices
        # transform_u! doesn't resize arrays - it only applies transformations in-place
        # The storage arrays retain their original allocated size
        @test size(odet.u_store) == size(u_orig)

        # Empty derivative stores must be skipped, not indexed into
        @test isempty(odet.du_store)
        @test isempty(odet.xi_s_store)
    end

    @testset "transform_u! with pre-filled derivative stores" begin
        # The galerkin-matched path supplies analytic derivatives before the fixup transforms,
        # so those arrays must be mixed by the same fixfac matrices as u_store.
        mpert = 2
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; mpert=mpert, numpert_total=mpert)
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(mpert, 10, 5, 2)

        odet.ifix = 1
        odet.step = 5
        odet.sing_flag[1] = false
        odet.fixstep[1] = 3
        odet.zeroed_idx[1] = Int[]
        odet.fixfac[1, 1, 1] = 1.0
        odet.fixfac[1, 2, 1] = 0.5
        odet.fixfac[2, 1, 1] = 0.0
        odet.fixfac[2, 2, 1] = 1.0
        odet.index[:, 1] = [1, 2]

        odet.du_store = zeros(ComplexF64, mpert, mpert, odet.step)
        odet.xi_s_store = zeros(ComplexF64, mpert, mpert, odet.step)
        for i in 1:odet.step
            odet.u_store[:, :, 1, i] .= ComplexF64(i)
            odet.u_store[:, :, 2, i] .= ComplexF64(i + 0.1)
            odet.du_store[:, :, i] .= ComplexF64(i + 0.2)
            odet.xi_s_store[:, :, i] .= ComplexF64(i + 0.4)
        end
        du_orig = copy(odet.du_store)
        xi_s_orig = copy(odet.xi_s_store)

        GeneralizedPerturbedEquilibrium.ForceFreeStates.transform_u!(odet, intr)

        @test size(odet.du_store) == size(du_orig)
        @test size(odet.xi_s_store) == size(xi_s_orig)
        @test !all(odet.du_store .== du_orig)
        @test !all(odet.xi_s_store .== xi_s_orig)
    end

    @testset "apply_gaussian_reduction!" begin
        # Initialize to random u
        mpert = 5
        ifix = 1
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(mpert, 10, 10, 10)
        odet.u = randn(ComplexF64, mpert, mpert, 2)
        odet.unorm = [norm(odet.u[:, i, 1]) for i in 1:mpert]
        odet.ifix = ifix
        odet.fixfac = zeros(ComplexF64, mpert, mpert, ifix)
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; numpert_total=mpert)

        # Save copy of original u and run
        u_orig = copy(odet.u)
        GeneralizedPerturbedEquilibrium.ForceFreeStates.apply_gaussian_reduction!(odet.u, odet, intr, false)

        # Very simple tests
        @test !all(odet.u .== u_orig)  # u should have changed
        @test all(abs.(diag(odet.fixfac[:, :, ifix])) .≈ 1)  # diagonal of fixfac = 1
        @test odet.fixstep[1] == odet.step - 1 # fixstep should be set
        @test odet.sing_flag[1] == false # sing_flag should match input

        mpert = 31
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(mpert, 10, 10, 10)
        # We'll load in Fortran data for u pulled before and after a fixup
        # Note that this was generated by manually setting
        # unorm = [norm(odet.u[:,i,1]) for i in 1:msol] in the Fortran to avoid
        # also having to save unorm0
        odet.u = load_u_matrix(joinpath(@__DIR__, "test_data", "u_prefixup.dat"))
        odet.unorm = [norm(odet.u[:, i, 1]) for i in 1:mpert]
        odet.ifix = ifix
        odet.fixfac = zeros(ComplexF64, mpert, mpert, ifix)
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; numpert_total=mpert)

        GeneralizedPerturbedEquilibrium.ForceFreeStates.apply_gaussian_reduction!(odet.u, odet, intr, false)

        u_fortran = load_u_matrix(joinpath(@__DIR__, "test_data", "u_postfixup.dat"))
        # test that the outputs are approximately equivalent (1e-3 seems ok to account for loading differences)
        @test all(abs.(odet.u .- u_fortran) .< 1e-3)

        # Test with a simple 2x2 case where we can predict the result
        mpert = 2
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(mpert, 10, 10, 10)

        # Set up a simple u matrix where first column has larger norm
        # u[:, 1, 1] = [3, 4] (norm = 5)
        # u[:, 2, 1] = [1, 0] (norm = 1)
        odet.u[:, 1, 1] .= [3.0 + 0.0im, 4.0 + 0.0im]
        odet.u[:, 2, 1] .= [1.0 + 0.0im, 0.0 + 0.0im]
        odet.u[:, :, 2] .= 0.0  # Set second equation to zero for simplicity

        odet.unorm = [norm(odet.u[:, i, 1]) for i in 1:mpert]
        odet.ifix = 1
        odet.fixfac = zeros(ComplexF64, mpert, mpert, 1)
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; numpert_total=mpert)

        u_before = copy(odet.u)

        GeneralizedPerturbedEquilibrium.ForceFreeStates.apply_gaussian_reduction!(odet.u, odet, intr, false)

        # After fixup:
        # - index should sort by norm: [1, 2] (largest first)
        @test odet.index[:, 1] == [1, 2]

        # - The largest element in the first column should be used as pivot
        # - The second column should be modified to eliminate that element
        # - fixfac should capture the elimination factor
        @test odet.fixfac[1, 1, 1] == 1.0  # Diagonal

        # The pivot element should not change
        pivot_idx = argmax(abs.(u_before[:, 1, 1]))
        @test abs(odet.u[pivot_idx, 1, 1] - u_before[pivot_idx, 1, 1]) < 1e-10
    end

    @testset "compute_solution_norms!" begin
        mpert = 2
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(mpert, 10, 10, 10)
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; mpert=mpert)
        ctrl = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesControl(; ucrit=10.0)

        # Case 1: Basic norm computation
        odet.u = zeros(ComplexF64, 2, 2, 2)
        odet.u[:, 1, 1] .= [3, 4]          # norm = 5
        odet.u[:, 2, 1] .= [0, 2]          # norm = 2

        GeneralizedPerturbedEquilibrium.ForceFreeStates.compute_solution_norms!(odet.u, odet, ctrl, intr, false)
        # After the first run with new=True (default), unorm0 should be set to unorm
        # and new should be false
        @test odet.unorm[1:intr.mpert] ≈ [5, 2]
        @test odet.unorm0 == odet.unorm
        @test odet.new == false

        # Case 2: Error on zero norm
        odet.u[:, 1, 1] .= 0
        odet.new = true
        @test_throws ErrorException GeneralizedPerturbedEquilibrium.ForceFreeStates.compute_solution_norms!(odet.u, odet, ctrl, intr, false)

        # Case 3: Normalization on second call
        odet.u[:, 1, 1] .= [3, 4]   # norm = 5
        odet.u[:, 2, 1] .= [0, 2]   # norm = 2
        odet.new = false
        GeneralizedPerturbedEquilibrium.ForceFreeStates.compute_solution_norms!(odet.u, odet, ctrl, intr, false)
        @test odet.unorm[1:intr.mpert] ≈ [1, 1]

        # Case 4: Trigger fixup via ucrit
        odet.unorm0 = ones(intr.mpert)
        odet.u[:, 1, 1] .= [1000, 0]   # large norm
        odet.u[:, 2, 1] .= [1, 0]      # small norm
        GeneralizedPerturbedEquilibrium.ForceFreeStates.compute_solution_norms!(odet.u, odet, ctrl, intr, false)
        @test odet.new == true  # implies fixup ran

        # Case 5: Trigger fixup via sing_flag
        odet.new = false
        odet.u[:, 1, 1] .= [1, 0]
        odet.u[:, 2, 1] .= [1, 0]
        GeneralizedPerturbedEquilibrium.ForceFreeStates.compute_solution_norms!(odet.u, odet, ctrl, intr, true)
        @test odet.new == true  # fixup triggered
    end

    @testset "OdeState construction" begin
        # Test basic OdeState initialization
        numpert_total = 5
        numsteps_init = 100
        numunorms_init = 20
        msing = 10

        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(numpert_total, numsteps_init, numunorms_init, msing)

        # Check fields are initialized correctly
        @test odet.numpert_total == numpert_total
        @test odet.numsteps_init == numsteps_init
        @test odet.numunorms_init == numunorms_init
        @test odet.msing == msing
        @test odet.step == 1
        @test odet.new == true
        @test odet.ifix == 0
        @test odet.nzero == 0

        # Check array dimensions
        @test size(odet.u) == (numpert_total, numpert_total, 2)
        @test size(odet.u_store) == (numpert_total, numpert_total, 2, numsteps_init)
        # Derivative stores start empty and are sized when materialized
        @test size(odet.du_store) == (numpert_total, numpert_total, 0)
        @test size(odet.xi_s_store) == (numpert_total, numpert_total, 0)
        @test odet.du_store_populated == false
        @test odet.u_store_el_basis == true
        @test length(odet.psi_store) == numsteps_init
        @test length(odet.q_store) == numsteps_init
        @test size(odet.ca_r) == (numpert_total, numpert_total, 2, msing)
        @test size(odet.ca_l) == (numpert_total, numpert_total, 2, msing)
        @test size(odet.fixfac) == (numpert_total, numpert_total, numunorms_init)
        @test length(odet.unorm) == numpert_total
        @test length(odet.unorm0) == numpert_total
    end

    @testset "interior start falls back to the fixed initialization" begin
        FFS = GeneralizedPerturbedEquilibrium.ForceFreeStates
        ex = joinpath(@__DIR__, "test_data", "regression_solovev_ideal_example")
        inputs = TOML.parsefile(joinpath(ex, "gpec.toml"))
        inputs["ForceFreeStates"]["verbose"] = false
        function axis_state(psilow; kwargs...)
            eq_inputs = copy(inputs["Equilibrium"])
            eq_inputs["psilow"] = psilow
            eq_config = GeneralizedPerturbedEquilibrium.Equilibrium.EquilibriumConfig(eq_inputs, ex)
            equil = GeneralizedPerturbedEquilibrium.Equilibrium.setup_equilibrium(eq_config, GeneralizedPerturbedEquilibrium.Equilibrium.SolovevConfig(inputs["SOL_INPUT"]))
            ctrl = FFS.ForceFreeStatesControl(; (Symbol(k) => v for (k, v) in inputs["ForceFreeStates"])..., kwargs...)
            intr = FFS.ForceFreeStatesInternal(; dir_path=ex)
            intr.nlow = ctrl.nn_low
            intr.nhigh = ctrl.nn_high
            intr.npert = 1
            FFS.sing_lim!(intr, ctrl, equil)
            FFS.sing_find!(intr, equil)
            intr.mlow = min(intr.nlow * equil.params.qmin, 0) - 4 - ctrl.delta_mlow
            intr.mhigh = trunc(Int, intr.nhigh * equil.params.qmax) + ctrl.delta_mhigh
            intr.mpert = intr.mhigh - intr.mlow + 1
            intr.numpert_total = intr.mpert * intr.npert
            mats = FFS.build_matrix_splines(equil, intr, FFS.make_metric(equil, intr.mpert))
            odet = FFS.OdeState(intr.numpert_total, ctrl.numsteps_init, ctrl.numunorms_init, intr.msing)
            return odet, ctrl, mats, equil, intr
        end
        # Near the axis the Frobenius start gives the regular solution: U₂ = I with a nonzero U₁.
        odet, ctrl, mats, equil, intr = axis_state(1e-4)
        FFS.initialize_el_at_axis!(odet, ctrl, mats, equil.profiles, intr)
        @test odet.u[:, :, 2] ≈ I
        @test any(!iszero, odet.u[:, :, 1])
        # An interior start switches to the fixed start (U₁ = 0, U₂ = I) and says so.
        odet, ctrl, mats, equil, intr = axis_state(0.3)
        @test_logs (:warn, r"fixed start") FFS.initialize_el_at_axis!(odet, ctrl, mats, equil.profiles, intr)
        @test iszero(odet.u[:, :, 1]) && odet.u[:, :, 2] ≈ I
        # The threshold is a control: raising it keeps the Frobenius start, and zero selects the fixed start silently.
        odet, ctrl, mats, equil, intr = axis_state(0.3; frobenius_psi_max=0.5)
        @test_logs FFS.initialize_el_at_axis!(odet, ctrl, mats, equil.profiles, intr)
        @test any(!iszero, odet.u[:, :, 1])
        odet, ctrl, mats, equil, intr = axis_state(1e-4; frobenius_psi_max=0.0)
        @test_logs FFS.initialize_el_at_axis!(odet, ctrl, mats, equil.profiles, intr)
        @test iszero(odet.u[:, :, 1]) && odet.u[:, :, 2] ≈ I
    end

    @testset "chunk_el_integration_bounds tests" begin
        # Helper to build a minimal control and internal structs
        ctrl = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesControl(; numsteps_init=10, numunorms_init=5, singfac_min=1e-4)

        # Case 1: No singular surfaces -> single chunk to edge
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; mpert=1, numpert_total=1)
        intr.msing = 0
        intr.psilim = 1.0

        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(1, ctrl.numsteps_init, ctrl.numunorms_init, intr.msing)
        odet.psifac = 0.0

        chunks = GeneralizedPerturbedEquilibrium.ForceFreeStates.chunk_el_integration_bounds(odet, ctrl, intr)
        @test length(chunks) == 1
        @test chunks[1].needs_crossing == false

        # Case 2: One singular surface within limits -> crossing chunk then edge
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; mpert=1, numpert_total=1)
        s = GeneralizedPerturbedEquilibrium.ForceFreeStates.SingType()
        s.psifac = 0.5
        s.n = [1]
        s.m = [1]
        s.q1 = 2.0
        intr.sing = [s]
        intr.msing = 1
        intr.psilim = 1.0
        intr.mlow = 1
        intr.mhigh = 1

        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(1, ctrl.numsteps_init, ctrl.numunorms_init, intr.msing)
        odet.psifac = 0.0

        chunks = GeneralizedPerturbedEquilibrium.ForceFreeStates.chunk_el_integration_bounds(odet, ctrl, intr)
        @test length(chunks) == 2
        @test chunks[1].needs_crossing == true
        @test chunks[2].needs_crossing == false
        # Ensure the first chunk ends just before the singular surface
        @test chunks[1].psi_end < intr.sing[1].psifac

        # Case 3: Multiple singular surfaces -> multiple crossing chunks
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; mpert=1, numpert_total=1)
        s1 = GeneralizedPerturbedEquilibrium.ForceFreeStates.SingType(; psifac=0.3, n=[1], m=[1], q1=1.5)
        s2 = GeneralizedPerturbedEquilibrium.ForceFreeStates.SingType(; psifac=0.6, n=[1], m=[1], q1=2.5)
        intr.sing = [s1, s2]
        intr.msing = 2
        intr.psilim = 1.0
        intr.mlow = 1
        intr.mhigh = 1
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(1, ctrl.numsteps_init, ctrl.numunorms_init, intr.msing)
        odet.psifac = 0.0
        ctrl = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesControl(; numsteps_init=10, numunorms_init=5, singfac_min=1e-6)
        chunks = GeneralizedPerturbedEquilibrium.ForceFreeStates.chunk_el_integration_bounds(odet, ctrl, intr)
        @test length(chunks) == 3
        @test all(c.needs_crossing == true for c in chunks[1:2])
        @test chunks[3].needs_crossing == false

        # Case 4: singfac_min == 0 should disable crossing logic -> single chunk
        intr = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesInternal(; mpert=1, numpert_total=1)
        intr.sing = [GeneralizedPerturbedEquilibrium.ForceFreeStates.SingType(; psifac=0.4, n=[1], m=[1], q1=2.0)]
        intr.msing = 1
        intr.psilim = 1.0
        odet = GeneralizedPerturbedEquilibrium.ForceFreeStates.OdeState(1, ctrl.numsteps_init, ctrl.numunorms_init, intr.msing)
        odet.psifac = 0.0
        ctrl = GeneralizedPerturbedEquilibrium.ForceFreeStates.ForceFreeStatesControl(; numsteps_init=10, numunorms_init=5, singfac_min=0.0)
        chunks = GeneralizedPerturbedEquilibrium.ForceFreeStates.chunk_el_integration_bounds(odet, ctrl, intr)
        @test length(chunks) == 1
        @test chunks[1].needs_crossing == false
    end

    @testset "EdgeScanState" begin
        FFS = GeneralizedPerturbedEquilibrium.ForceFreeStates
        # Default construction with sentinel values
        es = FFS.EdgeScanState(2, 0)
        @test es.numpert_total == 2
        @test es.N_edge == 0
        @test length(es.psi) == 0
        @test length(es.total_eigenvalue) == 0

        # Non-trivial N_edge: arrays initialized to NaN
        es2 = FFS.EdgeScanState(3, 5)
        @test es2.N_edge == 5
        @test length(es2.psi) == 5
        @test length(es2.q) == 5
        @test all(isnan, real.(es2.total_eigenvalue))
        @test all(isnan, real.(es2.plasma_energy))
        @test all(isnan, real.(es2.vacuum_energy))
        @test all(isnan, es2.vacuum_eigenvalue)

        # OdeState default construction (smoke test)
        ode = FFS.OdeState(2, 100, 10, 0)
        @test ode.numpert_total == 2
        @test ode.step == 1
    end
end

@testset "materialize_derivative_stores!" begin
    FFS = GeneralizedPerturbedEquilibrium.ForceFreeStates

    # Integrate a small ideal case and hand back everything the materializer needs.
    function setup_solovev_run()
        example_dir = joinpath(@__DIR__, "test_data", "regression_solovev_ideal_example")
        inputs = TOML.parsefile(joinpath(example_dir, "gpec.toml"))
        inputs["ForceFreeStates"]["verbose"] = false
        inputs["ForceFreeStates"]["integrator"] = "forward"
        inputs["ForceFreeStates"]["write_outputs_to_HDF5"] = false
        intr = FFS.ForceFreeStatesInternal(; dir_path=example_dir)
        ctrl = FFS.ForceFreeStatesControl(; (Symbol(k) => v for (k, v) in inputs["ForceFreeStates"])...)
        eq_config = GeneralizedPerturbedEquilibrium.Equilibrium.EquilibriumConfig(inputs["Equilibrium"], example_dir)
        sol_cfg = haskey(inputs, "SOL_INPUT") ? GeneralizedPerturbedEquilibrium.Equilibrium.SolovevConfig(inputs["SOL_INPUT"]) : nothing
        equil = GeneralizedPerturbedEquilibrium.Equilibrium.setup_equilibrium(eq_config, sol_cfg)
        intr.wall_settings = GeneralizedPerturbedEquilibrium.Vacuum.WallShapeSettings(; (Symbol(k) => v for (k, v) in inputs["Wall"])...)
        FFS.sing_lim!(intr, ctrl, equil)
        intr.nlow = ctrl.nn_low
        intr.nhigh = ctrl.nn_high
        intr.npert = 1
        FFS.sing_find!(intr, equil)
        intr.mlow = min(intr.nlow * equil.params.qmin, 0) - 4 - ctrl.delta_mlow
        intr.mhigh = trunc(Int, intr.nhigh * equil.params.qmax) + ctrl.delta_mhigh
        intr.mpert = intr.mhigh - intr.mlow + 1
        intr.numpert_total = intr.mpert * intr.npert
        metric = FFS.make_metric(equil, intr.mpert)
        mats = FFS.build_matrix_splines(equil, intr, metric)
        odet, _, _, _ = FFS.eulerlagrange_integration(ctrl, equil, mats, intr)
        return odet, ctrl, equil, mats, intr
    end

    odet, ctrl, equil, mats, intr = setup_solovev_run()
    # Untouched copy of the solution, for the column-transform check further down.
    odet_pristine = deepcopy(odet)

    @testset "fills the stores once" begin
        @test isempty(odet.du_store)
        @test !odet.du_store_populated
        @test FFS.materialize_derivative_stores!(odet, equil, mats, intr)
        @test odet.du_store_populated
        @test size(odet.du_store) == (intr.numpert_total, intr.numpert_total, odet.step)
        @test size(odet.xi_s_store) == (intr.numpert_total, intr.numpert_total, odet.step)
        @test all(isfinite, abs.(odet.du_store))
        @test all(isfinite, abs.(odet.xi_s_store))

        # Idempotent: a second call must not overwrite what is already there.
        du_first = copy(odet.du_store)
        @test FFS.materialize_derivative_stores!(odet, equil, mats, intr)
        @test odet.du_store == du_first
    end

    @testset "agrees with a direct kernel evaluation" begin
        npert = intr.numpert_total
        du = zeros(ComplexF64, npert, npert, 2)
        xi_s = zeros(ComplexF64, npert, npert)
        for istep in (1, odet.step ÷ 2, odet.step)
            psi = odet.psi_store[istep]
            u = odet.u_store[:, :, :, istep]
            FFS.el_derivatives!(du, u, false, equil, mats, intr, psi, Ref(1), Ref(1))
            FFS.compute_node_xi_s!(xi_s, @view(du[:, :, 1]), @view(u[:, :, 1]), mats, psi)
            @test odet.du_store[:, :, istep] == du[:, :, 1]
            @test odet.xi_s_store[:, :, istep] == xi_s
        end
    end

    @testset "commutes with a column transform" begin
        # The design relies on du(psi, u*T) == du(psi, u)*T, which is what makes it exact to
        # materialize after the Gaussian fixups and free-boundary normalization rather than
        # transforming stored derivatives alongside u_store.
        npert = intr.numpert_total
        T = Matrix{ComplexF64}(I, npert, npert) .+ 0.25 .* ComplexF64.(reshape(sin.(1:npert^2), npert, npert))
        odet_t = deepcopy(odet_pristine)
        for istep in 1:odet_t.step
            odet_t.u_store[:, :, 1, istep] = odet_t.u_store[:, :, 1, istep] * T
            odet_t.u_store[:, :, 2, istep] = odet_t.u_store[:, :, 2, istep] * T
        end
        @test FFS.materialize_derivative_stores!(odet_t, equil, mats, intr)

        for istep in (1, odet.step ÷ 2, odet.step)
            @test isapprox(odet_t.du_store[:, :, istep], odet.du_store[:, :, istep] * T; rtol=1e-10)
            @test isapprox(odet_t.xi_s_store[:, :, istep], odet.xi_s_store[:, :, istep] * T; rtol=1e-10)
        end
    end

    @testset "refuses a solution outside the Euler-Lagrange basis" begin
        odet.du_store_populated = false
        odet.du_store = Array{ComplexF64}(undef, intr.numpert_total, intr.numpert_total, 0)
        odet.u_store_el_basis = false
        @test !FFS.materialize_derivative_stores!(odet, equil, mats, intr)
        @test isempty(odet.du_store)
        @test !odet.du_store_populated
    end
end
